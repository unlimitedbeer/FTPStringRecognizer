
// -*- tab-width: 4; -*-

%{
// Initial revision
//
%}

%start MainMap::Preambula
%header Recognizer.h
%class Recognizer

%map MainMap
%%
Preambula
{
	readChar(c: const char)[ctxt.isPreambulaFinished(c)] Username { preambulaPassed(c); }
	readChar(c: const char)[ctxt.isPreambulaCorrect(c)] Preambula { preambulaReading(c); }
	readChar(c: const char) Error { showError(c); }
}
Username
{
	readChar(c: const char)[ctxt.isUsernameFinished(c)] Server { usernamePassed(c); }
	readChar(c: const char)[ctxt.isUsernameCorrect(c)] Username { usernameReading(c); }
	readChar(c: const char) Error { showError(c); }
}
Server
{
	readChar(c: const char)[ctxt.isServerFinished(c)] Domain { serverPassed(c); }
	readChar(c: const char)[ctxt.isServerCorrect(c)] Server { serverReading(c); }
	readChar(c: const char) Error { showError(c); }
}
Domain
{
	readChar(c: const char)[ctxt.isDomainFinished(c)] Zone { domainPassed(c); }
	readChar(c: const char)[ctxt.isStringFinished(c)] End { domainPassed(c); }
	readChar(c: const char)[ctxt.isDomainCorrect(c)] Domain { domainReading(c); }
	readChar(c: const char) Error { showError(c); }
}
Zone
{
	readChar(c: const char)[ctxt.isZoneFinished(c)] End { zonePassed(c); }
	readChar(c: const char)[ctxt.isZoneCorrect(c)] Zone { zoneReading(c); }
	readChar(c: const char) Error { showError(c); }
}
Error
{
 	nil nil {}
}
End
{
	nil nil {}
}


%%
